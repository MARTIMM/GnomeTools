use v6.d;
=begin pod
=TITLE GnomeTools::Gtk::ListView
=head1 Description

A listview is like a list where objects can be inserted horizontally or vertically. The listview is filled using a factory and is created in steps. The list is often partly visible and only asks for the objects to be created when they become visible.

There are several events which needs to be captured if complex objects must be created. This class has only one event which is C<activate> and can be set using C<.set-activate()>.

=head2 CSS classes

The B<GnomeTools::Gtk::ListView> class is placed in a B<Gnome::Gtk4::ScrolledWindow>. That object has a classname C<listview-window> and the B<GnomeTools::Gtk::ListView> object has a classname C<listview-tool>. Other CSS nodes are defined for L<Gnome::Gtk4::ListView|content-docs/api2/reference/Gtk4/ListView.html#CSS_nodes>.

=head2 Example

This example shows the easy way to make use of the class. The objects created are simple B<Label> objects with a text.

First define a helper class.
=begin code
class HelperObject {
  method show-select (
    GnomeTools::Gtk::ListView :$listview,
    GnomeTools::Gtk::Dialog :$dialog, :@items
  ) {
    $dialog.set-status($listview.get-selection.join(', '));
  }

  method selection-changed (
    UInt $position, @selections, GnomeTools::Gtk::Dialog :$dialog, :@items
  ) {
    my @rows = @selections.map: {@items.first( $_, :k)};
    $dialog.set-status("Rows '{@rows.join(', ')}' are selected");
  }
}

my HelperObject $helper .= new;
=end code

Instantiate the class and setup the B<GnomeTools::Gtk::ListView>. In this example the ListView is placed in a B<GnomeTools::Gtk::Dialog>.

=begin code
my GnomeTools::Gtk::Dialog $dialog .= new(
  :dialog-header('Test Dialog'), :add-statusbar
);

my @items = <class role method sub submethod for else unit package module>;

my GnomeTools::Gtk::ListView $listview .= new(:multi-select);
$listview.set-events;
$listview.set-selection-changed(
  :object($helper), :method<selection-changed>, :$dialog, :@items
);
$listview.append(|@items);
$dialog.add-content( 'Nice list', $listview);

# Buttons
$dialog.add-button(
  $helper, 'show-select', 'Get Selection',
  :$dialog, :@items, :$listview
);

$dialog.add-button( $dialog, 'destroy-dialog', 'Cancel');

$dialog.set-size-request( 400, 300);
$dialog.show-dialog;
=end code

=comment -----------------------------------------------------------------------

=head1 Methods

=head2 new

Instantiate the C<GnomeTools::Gtk::ListView> class.

=begin code
submethod BUILD (
  Bool :$multi-select = False, :$orientation = GTK_ORIENTATION_VERTICAL
)
=end code

=item $multi-select; Selection method. When True, more than one entry can be selected. By default False. Selections can be done a) by holding <CTRL> or <SHIFT> and click on the entries. b) by dragging the pointer over the entries (rubberband select).
=item $orientation; Orientation of the list. By default shown vertically.

The user callback interface must be like;
=begin code
method activate-list-item ( UInt $position, @selections, *%options )
=end code

Where;
=item $position; Position is the last clicked selection.
=item @selections; The list of selections in the ListView. Any named arguments C<*%options> given to C<.new()> are given to the method.

=comment -----------------------------------------------------------------------
=head2 set-activate

Define a callback routine for the C<activate> event. The C<activate> event is emitted when a row has been activated by the user. If an item is activatable, double-clicking on the item, using the Return key or calling C<.activate() in Gnome::Gtk4::Widget> will activate the item. Activating instructs the containing view to handle activation.

=begin code
method set-activate ( :$object, Str :$method, *%options )
=end code

=item $object; User object where methods are defined to process the events.
=item $method; Method in user C<$object> to call when event arrives.
=item *%options: Any user options. The options are given to the method in C<$object>.

The API of C<$method> must be

=begin code
$object."$method"( UInt $position, @selections, *%options);
=end code

=item $position; The position of the activated entry.
=item @selections; A list of the currently selected entries.
=item %options. Any remaining named arguments given to C<.new()> are passed to the method.
=end pod
