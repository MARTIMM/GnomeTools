use v6.d;
#use Test;

use NativeCall;

use GnomeTools::Gio::Menu;

use Gnome::Gtk4::Application:api<2>;
use Gnome::Gtk4::ApplicationWindow:api<2>;
use Gnome::Gtk4::Label:api<2>;

use Gnome::N::GlibToRakuTypes:api<2>;
use Gnome::N::N-Object:api<2>;

use Gnome::Gio::T-ioenums:api<2>;

use Gnome::Glib::T-error:api<2>;

#-------------------------------------------------------------------------------
class MenuWindow {

  my constant APP_ID is export = 'io.github.martimm.tests.menu-test';

  has Gnome::Gtk4::Application $!application;
  has Gnome::Gtk4::ApplicationWindow $!app-window;

  #-----------------------------------------------------------------------------
  submethod BUILD ( ) {

    $!application .= new-application( APP_ID, G_APPLICATION_DEFAULT_FLAGS);

    # Register all necessary signals
    $!application.register-signal( self, 'app-activate', 'activate');
    $!application.register-signal( self, 'shutdown', 'shutdown');

    # Now we can register the application.
    my $e = CArray[N-Error].new(N-Error);
    $!application.register( N-Object, $e);
    die $e[0].message if ?$e[0];
  }

  #-----------------------------------------------------------------------------
  method run ( --> Int ) {
    $!application.run( 0, gpointer)
  }

  #-----------------------------------------------------------------------------
  method app-activate ( ) {
    my GnomeTools::Gio::Menu $bar .= new;
    my GnomeTools::Gio::Menu $m1 .= new( :parent-menu($bar), :name<File>);
    $m1.item( 'Quit', self, 'fq');
    my GnomeTools::Gio::Menu $ms1 .= new(
      :parent-menu($m1), :section('sectie')
    );
    $ms1.item( 'Edit', self, 'ed1');
    $ms1.item( 'Store', self, 'st1');

    my GnomeTools::Gio::Menu $m2 .= new( :parent-menu($bar), :name<About>);
    $m2.item( 'About Me', self, 'a0');
    my GnomeTools::Gio::Menu $m3 .= new( :parent-menu($m2), :subname<Info>);
    $m3.item( 'About Ref', self, 'o1');
    $m3.item( 'About Help', self, 'o2');

    $bar.set-actions($!application);

    $!application.set-menubar($bar.get-menu);
    $!app-window .= new-applicationwindow($!application);
    $!app-window.set-show-menubar(True);

    $!app-window.present;
  }

  method fq ( N-Object $parameter ) { note 'File Quit'; }
  method ed1 ( N-Object $parameter ) { note 'File Edit'; }
  method st1 ( N-Object $parameter ) { note 'File Store'; }
  method a0 ( N-Object $parameter ) { note 'About Me'; }
  method o1 ( N-Object $parameter ) { note 'About Info Ref'; }
  method o2 ( N-Object $parameter ) { note 'About Info Help'; }
}

#-------------------------------------------------------------------------------
my MenuWindow $menu-window .= new;

exit($menu-window.run);

