use v6.d;
#use Test;

#use lib '/home/marcel/Languages/Raku/Projects/gnome-source-skim-tool/gnome-api2/gnome-gtk4/lib';

use GnomeTools::Gtk::ListView;
use GnomeTools::Gtk::Dialog;

use Gnome::Gtk4::Grid:api<2>;
use Gnome::Gtk4::Image:api<2>;
use Gnome::Gtk4::Label:api<2>;
use Gnome::Gtk4::T-enums:api<2>;

use Gnome::N::N-Object:api<2>;

%*ENV<IGNORE_GNOME_DEPRECATION_MESSAGES> = 1;

#-------------------------------------------------------------------------------
class HelperObject {
  method show-select (
    GnomeTools::Gtk::ListView :$listview,
    GnomeTools::Gtk::Dialog :$dialog,
    :%items
  ) {
    my @selections = %items.keys[$listview.get-selection];
    $dialog.set-status(@selections.join(', '));
  }

  method selection-changed (
    UInt $position, @selections, GnomeTools::Gtk::Dialog :$dialog
  ) {
    $dialog.set-status(
      [~] "$position ",
          $position (elem) @selections ?? 'added' !! 'removed',
          ", rows '{@selections.join(', ')}' are selected"
    );
  }

  method setup-list-item ( --> Gnome::Gtk4::Widget ) {
    with my Gnome::Gtk4::Label $label .= new-label {
      .set-hexpand(True);
      .set-justify(GTK_JUSTIFY_LEFT);
      .set-halign(GTK_ALIGN_START);
    }

    my Gnome::Gtk4::Image $image .= new-image;

    with my Gnome::Gtk4::Grid $grid .= new-grid {
      .set-column-spacing(10);
      .attach( $image, 0, 0, 1, 1);
      .attach( $label, 1, 0, 1, 1);
    }

    $grid
  }

  method bind-list-item ( Gnome::Gtk4::Grid() $grid, Str $text, :%items ) {
    my Gnome::Gtk4::Image() $image = $grid.get-child-at( 0, 0);
    my Gnome::Gtk4::Label() $label = $grid.get-child-at( 1, 0);
    $image.set-from-icon-name(
      <favorities folder-android-symbolic folder-network-symbolic>.roll
    );
    $label.set-text($text);
    if ?%items{$text} {
      with my Gnome::Gtk4::Label $extra-label .= new-label {
        .set-hexpand(True);
        .set-justify(GTK_JUSTIFY_LEFT);
        .set-halign(GTK_ALIGN_START);
        .set-text(%items{$text});
      }
      $grid.attach( $extra-label, 0, 1, 2, 1);
    }
  }

  method unbind-list-item ( Gnome::Gtk4::Grid() $grid, Str $text, :%items ) {
    if ?%items{$text} {
      my N-Object $nw = $grid.get-child-at( 0, 1);
      $grid.remove($nw);
    }
  }

  method teardown-list-item ( Gnome::Gtk4::Grid() $grid, Str $text ) { }
}

my HelperObject $helper .= new;

#-------------------------------------------------------------------------------
my GnomeTools::Gtk::Dialog $dialog .= new(
  :dialog-header('Test Dialog'), :add-statusbar
);

my %items = %(
  :class(''), :role(''), :method(''), :sub(''), :submethod(''),
  :for('Loop through a series of data'), :else('Decision making'),
  :unit(''), :package(''), :module('')
);

my GnomeTools::Gtk::ListView $listview .= new(
  :object($helper), :$dialog, :%items
);
for %items.keys -> $item {
  $listview.append($item);
}
$dialog.add-content( 'Nice list', $listview);

# Buttons
$dialog.add-button(
  $helper, 'show-select', 'Get Selection',
  :$dialog, :%items, :$listview
);

$dialog.add-button( $dialog, 'destroy-dialog', 'Cancel');

$dialog.set-size-request( 400, 300);
$dialog.show-dialog;

