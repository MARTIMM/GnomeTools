use v6.d;
#use Test;

use GnomeTools::Gtk::DropDown;
use GnomeTools::Gtk::Dialog;

use Gnome::Gtk4::Label:api<2>;

use Gnome::N::N-Object:api<2>;
use Gnome::N::X:api<2>;
#Gnome::N::debug(:on);


%*ENV<IGNORE_GNOME_DEPRECATION_MESSAGES> = 1;

my constant DropDown = GnomeTools::Gtk::DropDown;
my constant Dialog = GnomeTools::Gtk::Dialog;

#-------------------------------------------------------------------------------
class HelperObject {
  method show-select ( DropDown :$dropdown, Dialog :$dialog ) {
    my Str $selection = $dropdown.get-text;
    $dialog.set-status("'$selection' selected");
  }

  method selection-changed (
    DropDown :$dropdown1, DropDown :$dropdown2, Dialog :$dialog
  ) {
    my Str $selection = $dropdown2.get-text;
    $dialog.set-status("'$selection' selected");
    $dropdown1.select($selection);
  }
}

my HelperObject $helper .= new;

#-------------------------------------------------------------------------------
my @items = <class role method sub submethod for else unit package module>;

with my Dialog $dialog .= new(
  :dialog-header('Test Dialog'), :add-statusbar
) {
  my DropDown $dropdown1 .= new;
  for @items -> $item {
    $dropdown1.append($item);
  }
  .add-content( 'Select something 1', $dropdown1);

  my DropDown $dropdown2 .= new;
  $dropdown2.set-events(:object($helper), :$dialog, :$dropdown1, :$dropdown2);
  for @items.reverse -> $item {
    $dropdown2.append($item);
  }
  .add-content( 'Select something 2', $dropdown2);

  # Buttons
  .add-button(
    $helper, 'show-select', 'Get Selection 1', :$dialog, :dropdown($dropdown1)
  );

  .add-button(
    $helper, 'show-select', 'Get Selection 2', :$dialog, :dropdown($dropdown2)
  );

  .add-button( $dialog, 'destroy-dialog', 'Cancel');

  .show-dialog;
}
